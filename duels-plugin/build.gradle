import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.apache.tools.ant.filters.ReplaceTokens

clean.doFirst {
    delete "$rootDir/out/"
}

tasks.withType(ShadowJar) {
    destinationDir = file("$rootDir/out/")
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter(ReplaceTokens, tokens: [VERSION: project.version])
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations-java5:20.1.0'
    compileOnly 'org.projectlombok:lombok:1.16.20'
    implementation 'org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT'
    implementation 'com.mojang:authlib:1.5.21'
    implementation 'me.clip:placeholderapi:2.10.5'
    implementation 'com.SirBlobman.combatlogx:CombatLogX-API:10.0.0.0-SNAPSHOT'
    implementation name: 'MVdWPlaceholderAPI-3.1.1'
    implementation name: 'Vault-1.6.7'
    implementation name: 'EssentialsX'
    implementation name: 'CombatTagPlus'
    implementation name: 'PvPManager-3.7.16'
    implementation name: 'Factions-1.6.9.5-U0.1.14'
    implementation name: 'MassiveCore'
    implementation name: 'Factions'
    implementation name: 'MyPet-2.3.4'
    implementation name: 'BountyHunters-2.2.6'
    implementation name: 'SimpleClans-2.14.4.1'
    implementation name: 'LeaderHeadsAPI'

    implementation project(':duels-worldguard')
    implementation project(':duels-worldguard-v6')
    implementation project(':duels-worldguard-v7')
    implementation project(':duels-api')
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'org.inventivetalent.spiget-update:bukkit:1.4.2-SNAPSHOT'
}

shadowJar {
    archiveName = parent.name + '-' + project.version + '.jar'

    dependencies {
        include(dependency(':duels-worldguard'))
        include(dependency(':duels-worldguard-v6'))
        include(dependency(':duels-worldguard-v7'))
        include(dependency(':duels-api'))
        include(dependency('com.google.code.gson:.*'))
        include(dependency('org.inventivetalent.spiget-update:.*'))
    }
    def group = project.group.toString() + "." + parent.name.toLowerCase() + ".shaded."
    relocate 'org.inventivetalent.update.spiget', group + 'update.spiget'
    relocate 'com.google.gson', group + 'gson'
}

build.dependsOn(shadowJar)